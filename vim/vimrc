" The vimrc file of Evan Morikawa
" evan@evanmorikawa.com
" @e0m

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
"""""""""""""""""""""""""" Vundle Packages """""""""""""""""""""""""""""""
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Simply type :BundleInstall to install everything.

set nocompatible " Does not use old vi quirks
filetype off     " Required temporarily for vundle
set rtp+=~/.vim/bundle/vundle
call vundle#rc()


" Helps me move "
"""""""""""""""""
" Unload/delete/wipe a buffer, keep its window(s)
Bundle 'bufkill.vim'

" Extends % operator to match html tags and others.
Bundle 'tmhedberg/matchit'

" unimpaired.vim: pairs of handy bracket mappings
Bundle 'tpope/vim-unimpaired'

" A camel-case detecting motion
Bundle 'bkad/CamelCaseMotion'

" New text object for a function argument
Bundle 'vim-scripts/argtextobj.vim'

" Cursor editing in multiple locations
Bundle 'terryma/vim-multiple-cursors'

" Peepopen quickly finds and open files
Bundle 'topfunky/PeepOpen-EditorSupport', {'rtp': 'vim-peepopen/'}

" New text object at the same indent level
Bundle 'michaeljsmith/vim-indent-object'



" Helps me type "
"""""""""""""""""
" Write snippets in vim.
" <tab> after a snippet key
" http://vimcasts.org/episodes/meet-ultisnips/
Bundle 'SirVer/ultisnips'

" Snippets are separated from the engine.
Bundle 'honza/vim-snippets'

" Enable repeating supported plugin maps with '.'
Bundle 'tpope/vim-repeat'

" Quoting/parenthesizing made simple
Bundle 'tpope/vim-surround'

" Easily search for, substitute, & abbreviate multiple variants of a word
" https://github.com/tpope/vim-abolish
" http://vimcasts.org/episodes/enhanced-abbreviations-with-abolish/
Bundle 'tpope/vim-abolish'

" Easily comment in and out lines
" <leader>__
Bundle 'tomtom/tcomment_vim'

" Convert ruby blocks
" <leader>b
Bundle 'jgdavey/vim-blockle'

" Automatically complete brackets
Bundle 'jiangmiao/auto-pairs'

" Syntax checking hacks for vim
Bundle 'scrooloose/syntastic'

" Easy text exchange operator for Vim
" Mapped to cx
" http://vimcasts.org/episodes/swapping-two-regions-of-text-with-exchange-vim/
Bundle 'tommcdo/vim-exchange'

" Vim motions on speed. Replaces the <number> in <number><motion>
" <leader><leader><motion>
Bundle 'Lokaltog/vim-easymotion'

" True Sublime Text style multiple selections for Vim
" Mapped to C-n
Bundle 'terryma/vim-multiple-cursors'

" A parser for a condensed HTML format
" <c-e> after a block of sparkup-formatted pre-html
Bundle 'rstacruz/sparkup', {'rtp': 'vim/'}

" Awesome autocomplete
" http://valloric.github.io/YouCompleteMe/
if has("gui_macvim")
  " Once installed you have to also run the install.sh script inside of
  " the plugin folder. If you are using pyenv, there are additional steps.
  " See below in the YouCompleteMe section
  Bundle 'Valloric/YouCompleteMe'
end


" Helps me see "
""""""""""""""""
" JSX files. React JSX
" Bundle 'mxw/vim-jsx'

" HAML, SASS, SCSS support for Vim
Bundle 'tpope/vim-haml'

" Lean & mean status/tabline for vim that's light as air
Bundle 'bling/vim-airline'

" CJSX files. Coffeescript with React JSX
Bundle 'mtscout6/vim-cjsx'

" Markdown syntax for vim
Bundle 'tpope/vim-markdown'

" Less syntax support
Bundle 'groenewege/vim-less'

" Scala support
Bundle 'derekwyatt/vim-scala'

" Colors CSS tags with the appropriate color.
Bundle 'skammer/vim-css-color'

" Add CSS3 syntax support to vim's built-in `syntax/css.vim`
Bundle 'hail2u/vim-css3-syntax'

" Javascript & ES6 support for vim
Bundle 'othree/yajs.vim'
Bundle 'othree/es.next.syntax.vim'
" NOTE: As of 2/2/16 yajs indenting only worked if vim-javascript was
" BundleInstall'ed first, then deactivated with the folder still present
" Bundle 'emorikawa/vim-javascript'

" CoffeeScript support for vim
Bundle 'kchmck/vim-coffee-script'

" Typescript support"
Bundle 'leafgarland/typescript-vim'

" Shows indent level
Bundle 'nathanaelkane/vim-indent-guides'

" Solarized theme (only used for light-mode)
" Use `evan` theme for dark mode
Bundle 'altercation/vim-colors-solarized'

" Literate CoffeeScript support for vim
Bundle 'mintplant/vim-literate-coffeescript'


" Helps my workflow "
"""""""""""""""""""""
" Ruby on Rails power tools
Bundle 'tpope/vim-rails'

" Run Rspec specs from Vim
Bundle 'thoughtbot/vim-rspec'


" Helps me search "
"""""""""""""""""""
Bundle 'grep.vim'

" Faster, better search built on top of `ag` aka `the_silver_searcher`
Bundle 'rking/ag.vim'


" Helps me collaborate "
""""""""""""""""""""""""
" A Git wrapper so awesome, it should be illegal
Bundle 'tpope/vim-fugitive'

" Helps me track my work "
""""""""""""""""""""""""""
" Awesome time/productivity tracker.
Bundle 'wakatime/vim-wakatime'


""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
"""""""""""""""""""""""""""" KEY MAPPINGS """"""""""""""""""""""""""""""""
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Keymappings are ordered first by frequency of use.

let mapleader = ","


" Quick escaping
inoremap jk <Esc>
inoremap kj <Esc>
cnoremap jk <Esc>
cnoremap kj <Esc>

" Quick saving
noremap df :w<CR>
noremap fd :w<CR>
inoremap df <Esc>:w<CR>
inoremap fd <Esc>:w<CR>

" Easier movement around the page
noremap 0 ^
noremap k gk
noremap j gj
noremap <End> $
noremap <Home> 0

" Note, cmd-shift-j does line concatenation.
noremap <S-j> 3j
noremap <S-k> 3k
noremap <S-l> 5l
noremap <S-h> 5h

noremap <C-j> 3<C-e>
noremap <C-k> 3<C-y>
noremap <PageUp> <c-u>
noremap <PageDown> <c-d>

" Unix line shell navigation
cnoremap <C-p> <Up>
inoremap <C-e> <End>
cnoremap <C-e> <End>
cnoremap <C-n> <Down>
inoremap <C-a> <Home>
cnoremap <C-a> <Home>
cnoremap <C-k> <C-U>
inoremap <C-k> <esc>C

" Move lines up or down via 'alt/option/⌥ key + j/k'
" http://vim.wikia.com/wiki/Moving_lines_up_or_down
" On Mac you can't do <A-j> or <A-k>. Instead you use the hack below:
" alt-key fix: http://stackoverflow.com/a/15399297/793472
nnoremap ∆ :m .+1<CR>==
nnoremap ˚ :m .-2<CR>==
inoremap ∆ <Esc>:m .+1<CR>==gi
inoremap ˚ <Esc>:m .-2<CR>==gi
vnoremap ∆ :m '>+1<CR>gv=gv
vnoremap ˚ :m '<-2<CR>gv=gv

" And by lowercase everything I mean undo
noremap U u
vnoremap U <Esc>u

" Use null buffer with change command
noremap c "_c

"" bubble single lines
nnoremap <C-Up> [e
nnoremap <C-Down> ]e

"" bubble multiple lines
vnoremap <C-Up> [egv
vnoremap <C-Down> ]egv

" Move between buffers
noremap <c-h> <c-w>h
noremap <c-l> <c-w>l

" Quick quoting. Uses tpope/vim-surround
nnoremap <leader>" ysiw"e
nnoremap <leader>' ysiw'e

" A new motion that selects in the next or prev set of parenthesis
onoremap in( :<c-u>normal! f(vi(<cr>
onoremap il( :<c-u>normal! F)vi(<cr>

""""" FILE MANAGEMENT
" Peep open to search between files
if has("gui_macvim")
  map <C-e> <Plug>PeepOpen
end

" Close buffer but keep split open
" Uses bufkill.vim
noremap <C-Q> :BD<CR>

" Close buffer
noremap <C-c> :bd<CR>

" Rename the file. We do this via the git plugin. This only works in git
" directories.
nnoremap <Leader>r :call RenameFile()<cr>

" Delete the buffer from disk and close the buffer
" Uses bufkill.vim
nnoremap <Leader>dd :call delete(expand('%'))<CR>:BD<CR>

" Create (or edit) another file in the same directory as the current file
nnoremap <Leader>e :e <C-R>=escape(expand("%:p:h"),' ') . '/'<CR>


""""" AWESOME SEARCHING
"In visual mode when you press * or # to search for the current selection
vnoremap <silent> * :call VisualSearch('f')<CR>n<S-n>
vnoremap <silent> # :call VisualSearch('b')<CR>n<S-n>
vnoremap <silent> <C-f> :call VisualSearch('f')<CR>n<S-n>
vnoremap <silent> <D-F> :call VisualSearch('f')<CR>n<S-n>ygn:Gr <C-r>"<CR>

" Search and highlight what's under the cursor. Also automatically copies
" the word to both yank and the mac paste buffer thanks to
" clipboard=unnamed
nnoremap <C-f> yiw:Subvert/<C-r>"<CR><S-n>

" Search via grep what's under the cursor.
nnoremap <D-F> *<S-n>ye:Gr <C-r>"<CR>

" Unhighlight
noremap <S-Space> :nohl<CR>

" Space bar searching via Subvert
" Part of Tim Pope's Abolish plugin
" http://vimcasts.org/episodes/smart-search-with-subvert/
noremap <space> :Subvert/


""""" GIT COMMANDS
noremap gd :Gdiff<CR>
noremap ga :Gwrite<CR>
noremap gb :Gbrowse<CR>
noremap gs :Gstatus<CR>
noremap gc :Gcommit -a<CR>
noremap gl :Gdiff HEAD~1<CR>

"Reselect visual block after indent/outdent
vnoremap < <gv
vnoremap > >gv

" Edit Vimrc
" $MYVIMRC points to the ~/.vimrc file
nnoremap <leader>ev :e $MYVIMRC<CR>

" Edit UltiSnips for this file
" You must first symlink ~/.vim/UltiSnips to the master snips folder.
nnoremap <leader>es :UltiSnipsEdit<CR>

" Edit theme
" You must first symlink ~/.vim/UltiSnips to the master snips folder.
nnoremap <leader>et :e ~/.vim/colors/evan.vim<CR>

" Re-source vimrc
nnoremap <leader>sv :source $MYVIMRC<CR>

" Puts current file name in the mac paste buffer. Useful for operating on
" the file from the command line.
noremap <D-Y> :CopyTitle<CR><CR>

"Info about the character undernath cursor
nnoremap gi ga

" Toggles paste mode and shows value of the option
nnoremap <F2> :set invpaste paste?<CR>

" Don't accidentally open help when going for F2 or q
noremap <F1> <nop>

noremap <C-S> :%s/

nnoremap <silent> zz @=(foldlevel('.')?'za':'l')<CR>
vnoremap zz zf

"Sparkup to create HTML
let g:sparkupExecuteMapping = '<c-t>'

" How to create repeatable mappings
" http://vimcasts.org/episodes/creating-repeatable-mappings-with-repeat-vim/
nnoremap <silent> <Plug>TransposeCharacters xp
\:call repeat#set("\<Plug>TransposeCharacters")<CR>
nmap cp <Plug>TransposeCharacters

"Displays the tag type under the cursor. Really useful for generating colors
noremap <c-x> :echo "hi<" . synIDattr(synID(line("."),col("."),1),"name") . '> trans <'
\ . synIDattr(synID(line("."),col("."),0),"name") . "> lo<"
\ . synIDattr(synIDtrans(synID(line("."),col("."),1)),"name") . ">"<CR>

" Remove the Windows ^M - when the encodings gets messed up
noremap <Leader>m mmHmt:%s/<C-V><cr>//ge<cr>'tzt'm

"Change to light bg
noremap <leader>l :set background=light<cr>:colorscheme solarized<cr>

"Change to dark bg
noremap <leader>d :set background=dark<cr>:colorscheme evan<cr>

" Convert RSpec should to expect syntax
nnoremap <Leader>cse :call ConvertShouldToExpect()<CR>


""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
"""""""""""""""""""""""""""""" SETTINGS """"""""""""""""""""""""""""""""""
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
filetype plugin indent on " Turn on filetype after Vundle is setup
colorscheme evan
" colorscheme solarized
syntax enable

set wrap " Wrap lines
set exrc  " Allow per-project vimrc's
set list " Show characters for tabs, spaces, end of line
set ruler " Shows line & column numbers
set number " Turn on line numbers
set secure  " Prevent :autocmd, shell and write commands from being run inside project-specific .vimrc files unless they're owned by me
set smarttab " <Tab> inserts correct number of spaces
set wildmenu " Enhanced tab completion
set autoread " Reload file when a file is changed from the outside
set vb t_vb= " Disables visual bell on term
set hlsearch " Highlight search matches
set showmode " Says whether or not paste mode is turned on or off
set t_Co=256 " Enable colored terminal
let &t_Co=256 " Number of term colors
set linebreak " Wordwrapping doesn't break words in the middle
set incsearch " Incremental search as typing
set smartcase " Will switch to case sensitive if a cap is used
set showmatch " Shows matches on search
set expandtab " Replaces tabs with spaces
set tabstop=2 " Number of spaces that a tab counts for
set ignorecase " Case insensitive search but see next option
set autoindent " Enable automatic alignment during insertions
set noswapfile " Don't use swp files
set smartindent " Tries to recognize code and indent
set matchtime=2 " two-tenths of a second blink
set cmdheight=2 " The commandbar height
set shiftwidth=2 " Number of spaces to use for each (auto)indent
set textwidth=74 " Maximum line length at 74 characters. Trigged with gq
set history=1000 " History length of ':' commands
set laststatus=2 " Always show the status line
set encoding=utf8 " UTF-8 FTW
set shell=/bin/sh " Use bash shell
set guioptions-=T " Removes GUI bars
set guioptions-=l " Removes scrollbars
set guioptions-=r " Removes scrollbars
set guioptions-=b " Removes scrollbars
set colorcolumn=75 " Have a line of at 75 characters.
set pastetoggle=<F2> " Binds F2 to pastetoggling
set clipboard=unnamed " All yank commands now go to mac clipboard
let &t_Sf="\<Esc>[3%dm" " Term foreground color
let &t_Sb="\<Esc>[4%dm" " Term background color
let &t_te="\<Esc>[?47l" " Output of termcap mode
let &t_ti="\<Esc>[?47h" " Term in termcap mode
if has("gui_macvim")
  set backspace+=start,eol,indent " Make backspace work properly on vim
endif
set guifont=Menlo\ for\ Powerline " See Install folder
set wildmode=list:longest,full " Bash command line tab completion
set listchars=tab:\|\ ,trail:●,extends:>,precedes:<,nbsp:+

if has("gui_macvim")
  match LineTooLong /\%>74v.\+/ " 74 character lines.
endif

" We use Airline now. These are kept here for future reference.
" set statusline=%<%f\ %h%m%r%{fugitive#statusline()}%=%-14.(%l,%c%V%)\ %P
" set statusline=\ %{HasPaste()}%F%m%r%h\ %w\ \ CWD:\ %r%{CurDir()}%h\ \ \ Line:\ %l/%L:%c

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
"""""""""""""""""""""""""" CUSTOM COMMANDS """""""""""""""""""""""""""""""
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Removes all trailing whitespace
command! Whitespace %s/\s\+$//e

" Grep case insensitive
command! -nargs=1 Gri Grep -ErinI --exclude-dir=log --exclude-dir=tmp --exclude-dir=coverage --exclude-dir=.git <args> .

" Grep case sensitive
command! -nargs=1 Gr Grep -ErnI --exclude-dir=log --exclude-dir=tmp --exclude-dir=coverage --exclude-dir=.git --exclude-dir=node_modules --exclude-dir=saved_state --exclude-dir=Electron.app --exclude-dir=site-packages --exclude-dir=build/lib/maple --exclude-dir=fixtures --exclude-dir=maple/build --exclude=\*.min.js <args> .

" Grep through coffeescript
command! -nargs=1 Grc Grep -ErinI --include=\*.coffee --exclude-dir=log --exclude-dir=tmp --exclude-dir=coverage --exclude-dir=.git <args> .

" Grep through ruby
command! -nargs=1 Grr Grep -ErinI --include=\*.rb --exclude-dir=log --exclude-dir=tmp --exclude-dir=coverage --exclude-dir=.git <args> .

command! CopyTitle !echo "%\c" | pbcopy

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
""""""""""""""""""""" LANGUAGE SPECIFIC SETTINGS """""""""""""""""""""""""
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Coffeescript "
""""""""""""""""
augroup filetype_coffee
  autocmd!
  autocmd BufRead,BufNewFile *.coffee set ft=coffee
augroup END

" ES6 "
""""""""""""""""
augroup filetype_es6
  autocmd!
  autocmd BufRead,BufNewFile *.es6 set ft=javascript.jsx
augroup END


" Python "
""""""""""
augroup filetype_python
  autocmd!
  autocmd FileType python setlocal
    \ tabstop=4
    \ softtabstop=4
    \ shiftwidth=4
    \ textwidth=73

  autocmd BufWrite *.py :call DeleteTrailingWS() "Deletes trailing whitepace
  let python_highlight_all = 1
  autocmd FileType python syn keyword pythonDecorator True None False self

  autocmd BufNewFile,BufRead *.jinja setlocal syntax=htmljinja
  autocmd BufNewFile,BufRead *.mako setlocal ft=mako
  autocmd BufNewFile,BufRead *.less setlocal ft=less
augroup END

" HTML "
""""""""
augroup filetype_html
  autocmd!
  autocmd BufRead,BufNewFile *.erb set ft=eruby.html
  autocmd FileType html,eruby.html iabbrev <buffer> --- &mdash;
augroup END

" Bash "
""""""""
augroup filetype_bash
  autocmd!
  autocmd FileType sh set tw=0
augroup END

" Rabl "
""""""""
augroup filetype_rabl
  autocmd!
  autocmd BufRead,BufNewFile *.rabl set ft=ruby
  autocmd BufRead,BufNewFile *.rabl syn keyword rubyRabl node attribute object child collection attributes glue extends
  autocmd BufRead,BufNewFile *.rabl hi def link rubyRabl Function
augroup END

" RSpec "
""""""""
augroup filetype_rspec
  autocmd!
  autocmd BufRead,BufNewFile *_spec.rb syn keyword rubyRspecFn describe context expect is_expected before after shared_examples include_examples it_behaves_like
  autocmd BufRead,BufNewFile *_spec.rb syn keyword rubyRspecKw it let to to_not subject
  autocmd BufRead,BufNewFile *_spec.rb hi def link rubyRspecFn Function
  autocmd BufRead,BufNewFile *_spec.rb hi def link rubyRspecKw Statement
augroup END

" Markdown "
""""""""""""
augroup filetype_markdown
  autocmd!

  " change inside heading
  autocmd FileType markdown onoremap <buffer> ih :<c-u>execute "normal!?^==\\+$\r:nohlsearch\rkvg_"<cr>

  " change around heading
  autocmd FileType markdown onoremap <buffer> ah :<c-u>execute "normal! ?^==\\+\r:nohlsearch\rg_vk0"<cr>

augroup END

" Vimscript "
"""""""""""""
augroup filetype_vim
  autocmd!
  autocmd FileType vim setlocal foldmethod=marker
augroup END



""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
"""""""""""""""""""""""""" RUN CURRENT FILE """""""""""""""""""""""""""""
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Execute current file
nnoremap <F5> :call ExecuteFile()<CR>

" Will attempt to execute the current file based on the `&filetype`
" You need to manually map the filetypes you use most commonly to the
" correct shell command.
function! ExecuteFile()
  let filetype_to_command = {
  \   'javascript': 'node',
  \   'coffee': 'coffee',
  \   'python': 'python',
  \   'html': 'open',
  \   'sh': 'sh'
  \ }
  let cmd = get(filetype_to_command, &filetype, &filetype)
  call RunShellCommand(cmd." %s")
endfunction

" Enter any shell command and have the output appear in a new buffer
" For example, to word count the current file:
"
"   :Shell wc %s
"
" Thanks to: http://vim.wikia.com/wiki/Display_output_of_shell_commands_in_new_window
command! -complete=shellcmd -nargs=+ Shell call RunShellCommand(<q-args>)
function! RunShellCommand(cmdline)
  echo a:cmdline
  let expanded_cmdline = a:cmdline
  for part in split(a:cmdline, ' ')
     if part[0] =~ '\v[%#<]'
        let expanded_part = fnameescape(expand(part))
        let expanded_cmdline = substitute(expanded_cmdline, part, expanded_part, '')
     endif
  endfor
  botright new
  setlocal buftype=nofile bufhidden=wipe nobuflisted noswapfile nowrap
  call setline(1, 'You entered:    ' . a:cmdline)
  call setline(2, 'Expanded Form:  ' .expanded_cmdline)
  call setline(3,substitute(getline(2),'.','=','g'))
  execute '$read !'. expanded_cmdline
  setlocal nomodifiable
  1
endfunction



""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
"""""""""""""""""""""""""" UTILITY METHODS """""""""""""""""""""""""""""""
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
function! CurDir()
  let curdir = substitute(getcwd(), '/Users/amir/', "~/", "g")
  return curdir
endfunction

function! HasPaste()
  if &paste
    return 'PASTE MODE  '
  else
    return ''
  endif
endfunction

function! DeleteTrailingWS()
  exe "normal mz"
  %s/\s\+$//ge
  exe "normal `z"
endfunc

function! CmdLine(str)
  exe "menu Foo.Bar :" . a:str
  emenu Foo.Bar
  unmenu Foo
endfunction

function! VisualSearch(direction) range
  let l:saved_reg = @"
  execute "normal! vgvy"

  let l:pattern = escape(@", '\\/.*$^~[]')
  let l:pattern = substitute(l:pattern, "\n$", "", "")

  if a:direction == 'b'
    execute "normal ?" . l:pattern . "^M"
  elseif a:direction == 'gv'
    call CmdLine("vimgrep " . '/'. l:pattern . '/' . ' **/*.')
  elseif a:direction == 'f'
    execute "normal /" . l:pattern . "^M"
  endif

  let @/ = l:pattern
  let @" = l:saved_reg
endfunction

" Rename current file (thanks Gary Bernhardt)
function! RenameFile()
    let old_name = expand('%')
    let new_name = input('New file name: ', expand('%'), 'file')
    if new_name != '' && new_name != old_name
        exec ':saveas ' . new_name
        exec ':silent !rm ' . old_name
        redraw!
    endif
endfunction

function! ConvertShouldToExpect()
  let rspec_conversions = {
        \ 'should': 'to',
        \ 'should_not': 'not_to',
        \ 'should_receive': 'to receive',
        \ 'should_not_receive': 'not_to receive',
        \ }
 
  for [old, new] in items(rspec_conversions)
    execute "normal! " . ':%s/\v^(\s+)(.+)\.' . old . '>/\1expect(\2).' . new . '/ge' . "\<CR>"
  endfor
endfunction

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
"""""""""""""""""""""""""" PLUGIN SETTINGS """""""""""""""""""""""""""""""
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" Sparkup "
"""""""""""
let sparkupNextMapping = '<M-e>'

" Vim Indent Guides "
"""""""""""""""""""""
let g:indent_guides_auto_colors = 0
augroup indent_guides
  autocmd!
  autocmd BufRead,BufNewFile * IndentGuidesEnable
augroup END

" YouCompleteMe and UltiSnips "
""""""""""""""""""""""""""""""
" https://github.com/Valloric/YouCompleteMe/issues/759
" When compiling YCM, install.sh needs to be changed to point to
" libpython and the python include directory. It by default can't detect
" pyenv's path structure

" let g:ycm_path_to_python_interpreter = '/Users/evanmorikawa/.pyenv/versions/2.7.5/bin/python'
" https://groups.google.com/forum/#!topic/ycm-users/l-d6Wfe-kgk
" let g:ycm_path_to_python_interpreter = '/usr/bin/python'

" How to integrate UltiSnips and YouCompleteMe
let g:UltiSnipsExpandTrigger       = "<tab>"
let g:UltiSnipsJumpForwardTrigger  = "<tab>"
let g:UltiSnipsJumpBackwardTrigger = "<s-tab>"

" Enable tabbing through list of results
function! g:UltiSnips_Complete()
    call UltiSnips#ExpandSnippet()
    if g:ulti_expand_res == 0
        if pumvisible()
            return "\<C-n>"
        else
            call UltiSnips#JumpForwards()
            if g:ulti_jump_forwards_res == 0
               return "\<TAB>"
            endif
        endif
    endif
    return ""
endfunction

au InsertEnter * exec "inoremap <silent> " . g:UltiSnipsExpandTrigger . " <C-R>=g:UltiSnips_Complete()<cr>"

" Expand snippet or return
let g:ulti_expand_res = 0
function! Ulti_ExpandOrEnter()
    call UltiSnips#ExpandSnippet()
    if g:ulti_expand_res
        return ''
    else
        return "\<return>"
endfunction

" Set <space> as primary trigger
inoremap <return> <C-R>=Ulti_ExpandOrEnter()<CR>

" Syntastic "
"""""""""""""
let g:syntastic_ignore_files = ['\m.*_templates\.html\.erb']
let g:syntastic_enable_signs=1
let g:syntastic_auto_jump=0
let g:syntastic_stl_format = '[%E{Err: %fe #%e}%B{, }%W{Warn: %fw #%w}]'
let g:syntastic_python_checkers = ['flake8']
let s:eslint_path = system('PATH=$(npm bin):$PATH && which eslint_d')
let g:syntastic_javascript_eslint_exec = substitute(s:eslint_path, '^\n*\s*\(.\{-}\)\n*\s*$', '\1', '')
let g:jsx_ext_required = 0 " Allow JSX in normal JS files
let g:syntastic_javascript_checkers = ['eslint']
let g:syntastic_coffee_checkers = ['coffeelint']

" Airline "
"""""""""""
let g:airline_powerline_fonts = 1
let g:airline#extensions#whitespace#checks = [ 'indent' ]
let g:airline#extensions#fugitive#enabled = 1
let g:airline_theme_patch_func = 'AirlineThemePatch'
let g:airline_theme="tomorrow"
function! AirlineThemePatch(palette)
  " This is based off of the 'tomorrow' theme with some inspiration from
  " zenburn
  let s:IA = airline#themes#get_highlight('NonText')

  let s:N1 = airline#themes#get_highlight2(['DbgCurrent', 'bg'], ['Folded', 'fg'], 'bold')
  let s:N2 = airline#themes#get_highlight('Folded')
  let s:N3 = airline#themes#get_highlight('NonText')
  let a:palette.normal = airline#themes#generate_color_map(s:N1, s:N2, s:N3)

  let a:palette.insert.airline_c = s:IA
  let a:palette.insert.airline_x = s:IA
  let a:palette.replace.airline_c = s:IA
  let a:palette.replace.airline_x = s:IA
  let a:palette.visual.airline_c = s:IA
  let a:palette.visual.airline_x = s:IA

  let s:MOD = airline#themes#get_highlight2(['Error','bg'], ['NonText', 'bg'])
  let a:palette.normal_modified.airline_c = s:MOD

  let a:palette.inactive = airline#themes#generate_color_map(s:IA, s:IA, s:IA)
endfunction
